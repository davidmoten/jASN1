
configure(allprojects) {
  group = 'org.openmuc.jasn1'
  version = '1.1.3'

  apply plugin: 'java'

  repositories {
      mavenCentral()
      mavenRepo name: 'bintray-juherr-asn1', url: "http://dl.bintray.com/content/juherr/asn1"
  }

}

configure(subprojects) {

  apply plugin: 'eclipse'
  apply plugin: 'osgi'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.5
  targetCompatibility = 1.5

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
  }

  task copyLibs(type: Copy) {
    dependsOn(configurations.default.getAllArtifacts().getBuildDependencies())

    into "build/lib"
    from configurations.default
    from configurations.default.getAllArtifacts().getFiles()
  }

  build.dependsOn(copyLibs)

  eclipse.pathVariables([GRADLE_USER_HOME:file(gradle.gradleUserHomeDir)])
  tasks.eclipse.dependsOn(cleanEclipse)


  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }

        pom.withXml {
          def root = asNode();
          root.appendNode( 'url', 'http://www.openmuc.org/' )
          def jira = root.appendNode( 'issueManagement' )
          jira.appendNode( 'system', 'github' )
          jira.appendNode( 'url', 'https://github.com/juherr/jASN1/issues' )
          def scm = root.appendNode( 'scm' )
          scm.appendNode( 'url', 'https://github.com/juherr/jASN1/issues' )
          scm.appendNode( 'connection', 'scm:git:http://github.com/juherr/jASN1.git' )
          scm.appendNode( 'developerConnection', 'scm:git:https://github.com/juherr/jASN1.git' )
          def license = root.appendNode( 'licenses' ).appendNode( 'license' );
          license.appendNode( 'name', 'GNU Lesser General Public License' )
          license.appendNode( 'url', 'http://www.gnu.org/licenses/lgpl-3.0.html' )
          license.appendNode( 'distribution', 'repo' )
        }
      }
    }

    repositories {
      maven {
        name 'bintray-juherr-asn1-jASN1'
        url 'https://api.bintray.com/maven/juherr/asn1/jASN1/'
        credentials {
          username = bintray_username
          password = bintray_api_key
        }
      }

    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
  }
}

jar.enabled = false

task (tar, type: Tar) {
  dependsOn(subprojects.build)
  dependsOn(subprojects.javadoc)

  compression = Compression.GZIP

  into(project.archivesBaseName) {
    from('./') {
      include 'jasn1-ber/src/**/*'
      include 'jasn1-ber/build/lib/**/*'
      include 'jasn1-ber/build/docs/**/*'
      include 'jasn1-compiler/src/**/*'
      include 'jasn1-compiler/build/lib/**/*'
      include 'jasn1-compiler/build/docs/**/*'
      include 'jasn1-compiler/build.gradle'
      include 'doc/**/*'
      include 'licenses/**/*'
      include 'sample/**/*.java'
      include 'sample/**/*.txt'
      include 'sample/**/*.asn'
      include 'build.gradle'
      include 'settings.gradle'
    }
    from('./') {
      fileMode = 0755
      include 'jasn1-compiler/run-scripts/**/*'
    }
 
  }
}

